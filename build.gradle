buildscript {
	ext {
		springBootVersion = '3.2.5'
		restdocsApiSpecVersion = '0.17.1'
	}
}

plugins {
	id 'java'
	id 'java-library'
	id 'org.springframework.boot' version "${springBootVersion}"
	id 'io.spring.dependency-management' version '1.1.4'
	id 'com.diffplug.spotless' version '6.25.0'
	id 'jacoco'
	id 'org.sonarqube' version "5.0.0.4638"
	id 'com.epages.restdocs-api-spec' version "${restdocsApiSpecVersion}"
	id 'org.hidetake.swagger.generator' version '2.18.2'
}

wrapper {
	gradleVersion = '8.7'
	distributionUrl = distributionUrl.replace("bin", "all")
}

group = 'com.v.hana'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '17'
}

repositories {
	mavenCentral()
}

configurations {
	all{
		exclude module: 'spring-boot-starter-logging'
	}
}

ext {
	lombokVersion = '1.18.32'
}

apply from: "$rootDir/spotless.gradle"
apply from: "$rootDir/jacoco.gradle"
apply from: "$rootDir/sonarqube.gradle"

dependencies {
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter', version: springBootVersion
	testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: springBootVersion
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-log4j2', version: springBootVersion
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop', version: springBootVersion
	implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: springBootVersion
	implementation group: 'org.springdoc', name: 'springdoc-openapi-starter-webmvc-ui', version: '2.0.2'
	testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-launcher', version: '1.10.2'
	compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
	annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
	testCompileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
	testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion
	testImplementation group: 'org.springframework.restdocs', name: 'spring-restdocs-mockmvc'
	testImplementation group: 'com.epages', name: 'restdocs-api-spec-mockmvc' , version: restdocsApiSpecVersion
}

openapi3 {
	server {
		url = 'http://localhost:8080'
	}
	title = 'Hana V API'
	description = 'Hana V API 서버에서 제공하는 API 명세서입니다.'
	version = '1.0.0'
	outputFileNamePrefix = 'hana-v-api'
	format = 'json'

	outputDirectory = 'build/resources/main/static/docs'
}

tasks.named('bootRun') {
	dependsOn 'openapi3'
}

tasks.named('test') {
	useJUnitPlatform()

	testLogging {
		events 'PASSED', 'FAILED', 'SKIPPED'
		showStandardStreams = true
	}
}

tasks.register('preCommitHook') {
	doLast {
		def gitHooksDirectory = new File("$project.rootDir/.git/hooks/")
		if (!gitHooksDirectory.exists()) {
			gitHooksDirectory.mkdirs()
		}
		new File("$project.rootDir/.git/hooks", "pre-commit").text =
				"""#!/bin/bash
echo "테스트를 실행합니다."
./gradlew test
TEST_RESULT=\$?
if [ \$TEST_RESULT -eq 0 ]
then
	echo "테스트가 성공하였습니다."
	echo "코드를 포맷팅합니다."
	./gradlew spotlessApply
	STYLE_RESULT=\$?
	if [ \$STYLE_RESULT -eq 0 ]
	then
		echo "코드 포맷팅이 완료되었습니다."
	else
		echo "코드 포맷팅에 실패하였습니다." >&2
		exit 1
	fi
else
	echo "테스트가 실패하였습니다." >&2
	exit 1
fi
"""
		"chmod +x .git/hooks/pre-commit".execute()
	}
}

tasks.withType(GenerateSwaggerUI) {
	dependsOn 'openapi3'

	copy {
		from 'build/resources/main/static/docs'
		into 'src/main/resources/static/docs/'
	}
}

tasks.withType(JavaCompile) {
	options.compilerArgs << "-parameters"
}
